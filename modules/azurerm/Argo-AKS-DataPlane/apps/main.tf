# -------------------------------------------------------------------------------------
#
# Copyright (c) 2026, WSO2 LLC. (http://www.wso2.com). All Rights Reserved.
#
# This software is the property of WSO2 LLC. and its suppliers, if any.
# Dissemination of any information or reproduction of any material contained
# herein in any form is strictly forbidden, unless permitted by WSO2 expressly.
# You may not alter or remove any copyright or other notice from copies of this content.
#
# --------------------------------------------------------------------------------------

resource "kubernetes_namespace_v1" "this" {
  for_each = toset(concat(var.namespaces, [var.argocd_namespace, var.system_namespace]))

  metadata {
    name = each.value
  }
}

# One shared controller per data plane; tiers are isolated by RBAC, not separate installs.
resource "helm_release" "argo_workflows" {
  name             = "argo-workflows"
  repository       = var.argo_helm_repo
  chart            = "argo-workflows"
  version          = var.argo_workflows_chart_version
  namespace        = var.system_namespace
  create_namespace = false
  values           = var.argo_workflows_values

  depends_on = [kubernetes_namespace_v1.this]
}

resource "helm_release" "argo_events" {
  name             = "argo-events"
  repository       = var.argo_helm_repo
  chart            = "argo-events"
  version          = var.argo_events_chart_version
  namespace        = var.system_namespace
  create_namespace = false
  values           = var.argo_events_values

  depends_on = [kubernetes_namespace_v1.this]
}

resource "helm_release" "argocd" {
  count = var.install_argocd ? 1 : 0

  name             = "argocd"
  repository       = var.argocd_helm_repo
  chart            = "argo-cd"
  version          = var.argocd_chart_version
  namespace        = var.argocd_namespace
  create_namespace = false
  values           = var.argocd_values

  depends_on = [kubernetes_namespace_v1.this]
}

# The controller needs no identity: ClusterSecretStores authenticate via serviceAccountRef.

resource "kubernetes_namespace_v1" "external_secrets" {
  count = var.install_external_secrets ? 1 : 0

  metadata {
    name = var.eso_namespace
  }
}

resource "helm_release" "external_secrets" {
  count = var.install_external_secrets ? 1 : 0

  name             = "external-secrets"
  repository       = var.eso_helm_repo
  chart            = "external-secrets"
  version          = var.eso_chart_version
  namespace        = var.eso_namespace
  create_namespace = false

  depends_on = [kubernetes_namespace_v1.external_secrets]
}

resource "kubernetes_service_account_v1" "federated" {
  for_each = var.federated_service_accounts

  metadata {
    name      = coalesce(each.value.name, each.key)
    namespace = each.value.namespace
    annotations = {
      "azure.workload.identity/client-id" = each.value.client_id
    }
    labels = {
      "azure.workload.identity/use" = "true"
    }
  }

  depends_on = [kubernetes_namespace_v1.this]
}

# kubectl_manifest: kubernetes_manifest doesn't work reliably with exec auth.
locals {
  manifest_documents = flatten([
    for idx, m in var.manifest_files : [
      for doc_idx, doc in [
        for chunk in split("\n---\n", "\n${m.content != null ? m.content : templatefile(m.location, m.template_map)}") : chunk
        if trimspace(chunk) != ""
        ] : {
        key       = "${idx}-${doc_idx}"
        body      = doc
        namespace = m.namespace
      }
    ]
  ])
}

resource "kubectl_manifest" "this" {
  for_each = { for d in local.manifest_documents : d.key => d }

  yaml_body          = each.value.body
  override_namespace = each.value.namespace

  # Some manifests depend on state created later in this apply; waiting would deadlock.
  wait_for_rollout = false

  depends_on = [helm_release.argo_workflows, helm_release.argo_events, helm_release.argocd]
}

locals {
  kubectl_manifest_documents = flatten([
    for idx, m in var.kubectl_manifest_files : [
      for doc_idx, doc in [
        for chunk in split("\n---\n", "\n${m.content != null ? m.content : templatefile(m.location, m.template_map)}") : chunk
        if trimspace(chunk) != ""
        ] : {
        key       = "${idx}-${doc_idx}"
        body      = doc
        namespace = m.namespace
      }
    ]
  ])
}

resource "kubectl_manifest" "extra" {
  for_each = { for d in local.kubectl_manifest_documents : d.key => d }

  yaml_body          = each.value.body
  override_namespace = each.value.namespace

  wait_for_rollout = false

  depends_on = [helm_release.external_secrets, kubernetes_service_account_v1.federated, helm_release.argo_workflows, helm_release.argo_events, helm_release.argocd]
}

resource "local_file" "rendered_manifest" {
  for_each = var.rendered_manifest_files

  filename = "${path.module}/.rendered/${each.value.file_name}"
  content  = each.value.content
}
